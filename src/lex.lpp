%{

/*
    Copyright Â© 2012 Syd Logan. All Rights Reserved.

    Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

    1. Redistributions of source code must retain the above copyright notice,
this list of conditions and the following disclaimer.

    2. Redistributions in binary form must reproduce the above copyright
notice, this list of conditions and the following disclaimer in the
documentation and/or other materials provided with the distribution.

    3. The name of the author may not be used to endorse or promote products
derived from this software without specific prior written permission.

    THIS SOFTWARE IS PROVIDED BY Syd Logan "AS IS" AND ANY EXPRESS OR IMPLIED
WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO
EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER
IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
POSSIBILITY OF SUCH DAMAGE.
*/

#include "json.h"
#include "jsonobj.h"

%}

%option noyywrap
%option reentrant
%option bison-bridge
%option yylineno 
%option header-file="lex.h"

letter                  [a-zA-Z_]
digit                   [0-9]
letter_or_digit         [a-zA-Z_0-9]
white_space             [ \t\n]

%%

\"(\\.|[^\\"])*\"            {yylval->obj = new JsonValue(yytext); return tok_string;}
[-+]?{digit}+                {yylval->obj = new JsonValue(atol(yytext)); return tok_number;}
[-+]?{digit}+"."{digit}+     {yylval->obj = new JsonValue(atof(yytext)); return tok_float;}
[-+]?{digit}*\.{digit}+[eE][+-]?{digit}+ |
[-+]?{digit}+\.{digit}*[eE][+-]?{digit}+ {yylval->obj = new JsonValue(atof(yytext)); return tok_float;}
"{"                         {yylval->obj = new JsonValue(); static_cast<JsonValue *>(yylval->obj)->SetType(JsonType_Object); return tok_leftbrace;}
"}"                         {return tok_rightbrace;}
":"                         {return tok_colon;}
","                         {return tok_comma;}
"["                         {yylval->obj = new JsonValue(); static_cast<JsonValue *>(yylval->obj)->SetType(JsonType_Array); return tok_leftbracket;}
"]"                         {return tok_rightbracket;}
"true"                      {yylval->obj = new JsonValue(true); return tok_true; }
"false"                     {yylval->obj = new JsonValue(false); return tok_false;}
"null"                      {JsonValue *v = new JsonValue((long) 0); v->SetType(JsonType_Null); yylval->obj = v; return tok_null;}
{white_space}+              {}
.                           {return tok_unknown;}
